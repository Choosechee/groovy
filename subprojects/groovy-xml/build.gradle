/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
plugins {
    id 'org.apache.groovy-library'
}

dependencies {
    api rootProject // MarkupBuilder extends BuilderSupport...

    testImplementation projects.groovyTest
    testImplementation "xmlunit:xmlunit:${versions.xmlunit}"
    testImplementation("org.spockframework:spock-core:${versions.spock}") {
        exclude group: 'org.codehaus.groovy'
    }

    testRuntimeOnly projects.groovyAnt // for JavadocAssertionTests
    testRuntimeOnly(project(':')) {
        because 'Tests are using Grapes'
        capabilities {
            requireCapability 'org.apache.groovy:groovy-grapes'
        }
    }
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${versions.junit5}") {
        because 'for JUnit 3/4 tests as well as JUnit 5'
    }
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:${versions.junit5Platform}") {
        exclude(group: 'org.apiguardian', module: 'apiguardian-api')
    }
}

plugins.withId('eclipse') {
    eclipse.classpath.file.whenMerged {
        entries.removeAll { entry -> entry.path in ['/groovy-ant', '/groovy-groovydoc'] }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty 'spock.iKnowWhatImDoing.disableGroovyVersionCheck', 'true'
}

groovyLibrary {
    moduleDescriptor {
        extensionClasses = 'org.apache.groovy.xml.extensions.XmlExtensions'
    }
}
